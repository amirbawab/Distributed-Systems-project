apply plugin: 'application'
version = '1.0'
mainClassName = "ResImpl/ResourceManagerImpl"

task rmiregistry(type: Exec) {
    doFirst {
        // Set the working directory to the classpath
        workingDir "${sourceSets.main.java.outputDir}"

        // Check if running on windows
        if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
            throw new GradleException('The program was not tested on Windows.\nPlease start rmiregistry manually: ' +
                    "start rmiregistry ${serverPort}")
        } else {
            println "Starting rmiregistry at port ${serverPort} ..."
        }
    }

    commandLine "rmiregistry", serverPort
}

run {
    doFirst {
        copy {
            from "${rootDir}/etc/policy/"
            into "${buildDir}"
            include 'server.policy'
            expand('classpath': "${sourceSets.main.java.outputDir}")
        }
    }

    args "${serverPort}"
    jvmArgs([
            "-Djava.security.policy=${project.buildDir}/server.policy",
            "-Djava.rmi.server.codebase=file:${sourceSets.main.java.outputDir}"
    ])
}

// Managing dependencies
//run.dependsOn(rmiregistry)
